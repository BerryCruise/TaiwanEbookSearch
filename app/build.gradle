apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "liou.rayyuan.ebooksearchtaiwan"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode getVersionCodeTimeStamps()
        versionName app_version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystorePath)
            keyAlias keystoreAlias
            storePassword storePass
            keyPassword keyPass
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.all {
        buildConfigField 'String', 'HOST_URL', HOST
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

static def getVersionCodeTimeStamps() {
    def date = new Date()
    def formattedDate = date.format('yyMMddHHmm')
    def result = formattedDate.toInteger()
    return result
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(":chromecustomtabhelper")
    implementation "com.android.support:appcompat-v7:${support_library}"
    implementation "com.android.support:design:${support_library}"
    implementation "com.android.support:cardview-v7:${support_library}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.facebook.fresco:fresco:1.5.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
