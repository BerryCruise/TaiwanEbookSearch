apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "liou.rayyuan.ebooksearchtaiwan"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode getVersionCodeTimeStamps()
        versionName app_version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystorePath)
            keyAlias keystoreAlias
            storePassword storePass
            keyPassword keyPass
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.all {
        buildConfigField 'String', 'HOST_URL', HOST
        buildConfigField 'String', 'AD_MOB_ID', ADMOB_ID
        resValue 'string', 'AD_MOB_UNIT_ID', ADMOB_UNIT_ID
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

static def getVersionCodeTimeStamps() {
    def date = new Date()
    def formattedDate = '19' + date.format('yyMMddHH')
    def result = formattedDate.toInteger()
    return result
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.github.YuanLiou:ChromeCustomTabHelper:1.0'
    implementation "com.android.support:appcompat-v7:${support_library}"
    implementation "com.android.support:design:${support_library}"
    implementation "com.android.support:cardview-v7:${support_library}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'com.google.code.gson:gson:2.8.4'

    // Fresco
    implementation "com.facebook.fresco:fresco:$fresco_version"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$fresco_version"

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:1.1.1"

    // Java8 support for Lifecycles
    implementation "android.arch.lifecycle:common-java8:1.1.1"

    // Firebase
    implementation "com.google.firebase:firebase-core:${firebase_version}"
    implementation "com.google.firebase:firebase-ads:${firebase_version}"
    implementation "com.google.firebase:firebase-ml-vision:16.0.0"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

    // retrofit
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit_version}"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    // end retrofit

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

// Firebase config needs to put on bottom
apply plugin: 'com.google.gms.google-services'
