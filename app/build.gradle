apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.akaita.android.easylauncher'
apply from: '../gradle/detekt.gradle'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "liou.rayyuan.ebooksearchtaiwan"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getVersionCodeTimeStamps()
        versionName app_version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(keystorePath)
            keyAlias keystoreAlias
            storePassword storePass
            keyPassword keyPass
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.all {
        buildConfigField 'String', 'HOST_URL', HOST
        buildConfigField 'String', 'AD_MOB_ID', ADMOB_ID
        resValue 'string', 'AD_MOB_UNIT_ID', ADMOB_UNIT_ID
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

easylauncher {
    iconNames "@mipmap/ic_launcher"
    foregroundIconNames "@mipmap/ic_launcher_foreground"

    buildTypes {
        debug {
            filters = [
                    redRibbonFilter("WEBY")
            ]
        }
    }
}

static def getVersionCodeTimeStamps() {
    def date = new Date()
    def formattedDate = '19' + date.format('yyMMddHH')
    def result = formattedDate.toInteger()
    return result
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.github.YuanLiou:chrome-custom-tab-helper:1.1b1'

    // region Android X Libraries
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.recyclerview:recyclerview:${androidx_version}"
    implementation "androidx.cardview:cardview:${androidx_version}"
    implementation "androidx.preference:preference:${androidx_version}"
    implementation "com.google.android.material:material:${androidx_version}"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    // Java8 support for Lifecycles
    implementation "androidx.lifecycle:lifecycle-common-java8:2.0.0"
    // KTX
    implementation "androidx.core:core-ktx:1.0.1"
    implementation "androidx.fragment:fragment-ktx:1.0.0"
    // endregion of Android X Libraries

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.1'

    // Fresco
    implementation "com.facebook.fresco:fresco:$fresco_version"
    implementation "com.facebook.fresco:imagepipeline-okhttp3:$fresco_version"

    // Firebase
    implementation "com.google.firebase:firebase-core:16.0.3"
    implementation "com.google.firebase:firebase-ads:15.0.1"
    implementation "com.google.firebase:firebase-ml-vision:17.0.0"
    implementation "com.google.firebase:firebase-config:16.0.0"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.6'

    // Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:${retrofit_version}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofit_version}"
    implementation 'com.squareup.okhttp3:logging-interceptor:3.11.0'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.google.code.gson:gson:2.8.5'

    // Koin
    implementation "org.koin:koin-android:${koin_version}"

    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}

// Firebase config needs to put on bottom
apply plugin: 'com.google.gms.google-services'
